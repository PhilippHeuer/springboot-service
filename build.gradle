plugins {
    id 'java'
    id 'eclipse'
    id 'idea'
    id 'io.spring.dependency-management' version '1.0.5.RELEASE'
    id 'org.springframework.boot' version '2.1.2.RELEASE'
    id 'com.gorylenko.gradle-git-properties' version '2.0.0'
}

group = 'com.github.philippheuer.springboot'
description = 'A template for spring boot based microservices.'
version = '0.1.0'

repositories {
    jcenter()
    maven { url 'https://dl.bintray.com/philippheuer/maven' }
    maven { url 'http://oss.jfrog.org/artifactory/oss-snapshot-local/' }
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

dependencies {
    // Spring Boot - Web
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-webflux'
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-actuator'

    // Swagger
    compile group: 'com.github.philippheuer.springboot', name: 'springboot-webfluxswagger', version: '0.0.4'

    // Spring Boot - Test
    testCompile group: 'org.springframework.boot', name: 'spring-boot-starter-test'

    // Lombok
    compileOnly group: 'org.projectlombok', name: 'lombok'
}

/**
 * Allows @...@ style placeholders in the application.yml configuration file
 */
import org.apache.tools.ant.filters.ReplaceTokens
processResources {
    with copySpec {
        from 'src/main/resources'
        include '**/application*.yml'
        include '**/application*.yaml'
        include '**/application*.properties'
        project.properties.findAll().each {
            prop ->
                if (prop.value != null) {
                    filter(ReplaceTokens, tokens: [ (prop.key): prop.value])
                    filter(ReplaceTokens, tokens: [ ('project.' + prop.key): prop.value])
                }
        }
    }
}
